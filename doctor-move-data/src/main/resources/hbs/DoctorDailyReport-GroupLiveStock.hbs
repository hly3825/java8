-- 获取猪群日存栏统计(阶段仔猪)
-- 创建仔猪存栏临时表
CREATE TABLE #tempGroup (
farrowCount int,  -- 产房仔猪
nurseryCount int, -- 保育猪
fattenCount int,  -- 育肥猪
houbeiCount int,  -- 后备母猪
sumat DATETIME    -- 统计时间
)
DECLARE @i INT
SET @i = 0
WHILE @i < {{index}}   -- 循环index天的数据
BEGIN
DECLARE @sumDate DATETIME
SET @sumDate = convert(DATETIME, convert(varchar(100), getdate(), 23) + ' 23:59:59', 120) - @i

-- 插入临时表
INSERT INTO #tempGroup(farrowCount, nurseryCount, fattenCount, houbeiCount, sumat)

SELECT
CASE WHEN q.pigTypeName = '分娩母猪' THEN sum(q.quantity) ELSE 0 END AS farrowCount,  -- 产房仔猪
CASE WHEN q.pigTypeName = '保育猪' THEN sum(q.quantity) ELSE 0 END AS nurseryCount,   -- 保育猪
CASE WHEN q.pigTypeName = '育肥猪' THEN sum(q.quantity) ELSE 0 END AS fattenCount,    -- 育肥猪
CASE WHEN q.pigTypeName = '后备母猪' THEN sum(q.quantity) ELSE 0 END AS houbeiCount,   -- 后备母猪
convert(DATETIME, convert(varchar(100), getdate(), 23) + ' 00:00:00', 120) - @i as sumat
FROM
(
SELECT
p.OID                         AS groupOutId,
p.TypeName                    AS pigTypeName,
ISNULL(b.InventoryOfGroup, 0) AS quantity
FROM
(
SELECT OID, TypeName
FROM view_GainCardList
WHERE gainopendate <= @sumDate -- 转换下时间到天末
AND FarmOID = '{{farmOutId}}'
AND TypeName IN ('育肥猪', '保育猪', '分娩母猪', '后备母猪')
) AS p

-- 获得存栏数量
LEFT JOIN
(
SELECT
iGain.relmainOID              AS relmainOID,
InCount - ISNULL(OutCount, 0) AS InventoryOfGroup
FROM
(
SELECT
--统计到结束日期为止的转入猪只数
relmainOID,
SUM(ISNULL(NumberOfPigs, 0)) AS InCount
FROM view_EventListGain
WHERE
EventDate <= @sumDate
AND Operator = '+'
GROUP BY relmainOID, Operator
) AS iGain
LEFT JOIN
(
SELECT
--统计到结束日期为止的转出猪只数
relmainOID,
SUM(ISNULL(NumberOfPigs, 0)) AS OutCount
FROM view_EventListGain
WHERE
EventDate <= @sumDate
AND Operator = '-'
GROUP BY relmainOID, Operator
) AS oGain
ON iGain.relmainOID = oGain.relmainOID
) AS b
ON p.OID = b.relmainOID
) AS q
GROUP BY q.pigTypeName


SET @i = @i + 1
END

-- 查询统计的结果
SELECT
sumat,
sum(farrowCount) AS farrowCount,
sum(nurseryCount) AS nurseryCount,
sum(fattenCount) AS fattenCount,
sum(houbeiCount) AS houbeiCount
FROM #tempGroup
GROUP BY sumat

DROP TABLE #tempGroup